buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }
}

group 'io.zhudy.spring'
archivesBaseName 'spring-jdbc-ext'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    maven {
        url 'http://maven.aliyun.com/nexus/content/groups/public/'
    }
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.16"

    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.springframework:spring-jdbc:4.1.7.RELEASE"
    compile "com.github.jsqlparser:jsqlparser:0.9.6"

    testCompile "ch.qos.logback:logback-classic:1.2.1"
    testCompile "org.testng:testng:6.10"
    testCompile "org.springframework:spring-test:4.1.7.RELEASE"
    testCompile "org.springframework:spring-context:4.1.7.RELEASE"
    testCompile "org.xerial:sqlite-jdbc:3.16.1"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Zhudy Spring Jdbc Extension'
                packaging 'jar'
                description """Spring Jdbc Extension"""
                url 'https://github.com/zhudyos/spring-jdbc-ext'

                scm {
                    connection 'scm:git:https://github.com/zhudyos/spring-jdbc-ext.git'
                    developerConnection 'scm:git:git@github.com:zhudyos/spring-jdbc-ext.git'
                    url 'https://github.com/zhudyos/spring-jdbc-ext'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'kevin'
                        name 'Kevin Zou'
                        email 'kevinz@weghst.com'
                    }
                }
            }
        }
    }
}

